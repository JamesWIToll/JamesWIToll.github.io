<!DOCTYPE html>
<html lang="" style="height: 100%;">
  <head>
    <meta charset="UTF-8">
    <link rel="icon" href="/icoicon.png" />
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>James Tolliver Systems Programmer</title>
  </head>
  <body style="min-height:100%;">
    <div id="app"></div>
    <script type="module" src="/src/main.js"></script>

    <script type="x-shader/x-vertex" id="vertexShader">
        #version 300 es
        precision mediump float;

        layout (location = 0) in vec3 aPos;
        layout (location = 1) in vec4 aColor;
        layout (location = 2) in vec3 aNormal;
        layout (location = 3) in vec2 aTex0;
        layout (location = 4) in vec2 aTex1;
        layout (location = 5) in vec2 aTex2;
        layout (location = 6) in vec2 aTex3;
        layout (location = 7) in vec2 aTex4;

        uniform mat4 uModel;
        uniform mat4 uView;
        uniform mat4 uProjection;

        out vec2[5] TexCoords;
        out vec3 Normal;
        out vec3 WorldPos;
        out vec4 Color;

        void main() {
            gl_Position = uProjection * uView * uModel * vec4(aPos, 1.0f);
            TexCoords =  vec2[](aTex0, aTex1, aTex2, aTex3, aTex4);
            Normal = aNormal;
            Color =  aColor;
            WorldPos = vec3((uModel * vec4(aPos, 1.0f)).xyz);
        }
    </script>
    <script type="x-shader/x-fragment" id="fragmentShader">
        #version 300 es
        precision mediump float;
        out vec4 FragColor;
        in vec2[5] TexCoords;
        in vec3 Normal;
        in vec3 WorldPos;
        in vec4 Color;

        const float PI = 3.14159;

        struct Light {
            vec3 color;
            vec3 posDir;
        };

        uniform sampler2D uLines1;
        uniform sampler2D uLines2;
        uniform bool uCrosshatch;
        uniform bool uQuantizeColors;

        uniform int uHatchSize;
        uniform int uColorQuantity;

        uniform sampler2D uBaseTexture;
        uniform vec4 uBaseColor;
        uniform bool uUseDiffTexture;
        uniform float uMetallicness;
        uniform float uRoughness;
        uniform vec3 uCamPos;

        #define NR_LIGHTS 10
        uniform Light[NR_LIGHTS] uLights;

        float DistributionGGX(vec3 N, vec3 H, float roughness){
            float a2 =  roughness*roughness;
            float NdotH = max(dot(N, H), 0.0);
            float NdotH2 = NdotH*NdotH;

            float num = a2;
            float denom = (NdotH2 * (a2 - 1.0) + 1.0);
            denom = PI * denom * denom;

            return num / denom;
        }

        float GeometrySchlickGGX(float NdotV, float roughness){
            float num = NdotV;
            float denom = NdotV * (1.0 - roughness) + roughness;
            return num / denom;
        }

        float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness){
            float NdotV = max(dot(N, V), 0.0);
            float NdotL = max(dot(N, L), 0.0);
            float ggx1 = GeometrySchlickGGX(NdotV, roughness);
            float ggx2 = GeometrySchlickGGX(NdotV, roughness);

            return ggx1 * ggx2;
        }

        vec3 FresnelShlick(float cosTheta, vec3 F0){
            return F0 + (1.0 - F0) * pow(1.0 - cosTheta, 5.0);
        }

        float QuantizeChannel(float f, float num){
            float res = f;

            float currFloor = 0.0;
            float currCiel = 1.0/num;

            while (currCiel <= 1.0){
                if ( res <= currFloor){
                    res = currFloor;
                } else if (res <= currCiel) {
                    res = currCiel;
                    break;
                }

                currFloor = currCiel;
                currCiel += 1.0/num;
            }

            if (res > currCiel) {
                res = currCiel;
            }

            return res;
        }

        vec4 calcPBR(vec4 baseColor){
            vec3 result = baseColor.rgb;
            float alpha = baseColor.a;

            vec3 N = normalize(Normal);
            vec3 V = normalize(uCamPos - WorldPos);

            vec3 F0 = vec3(0.04);
            F0 = mix(F0, result, uMetallicness);

            vec3 Lo = vec3(0.0);
            for (int i = 0; i < NR_LIGHTS; i++) {
                //radiance
                vec3 L = normalize(uLights[i].posDir - WorldPos);
                vec3 H = normalize(V + L);
                float distance = length(uLights[i].posDir - WorldPos);
                float attenuation = 1.0 / (distance*distance);
                vec3 radiance = uLights[i].color * attenuation;


                //cook-torrance brdf
                float NDF = DistributionGGX(N, H, uRoughness);
                float G = GeometrySmith(N, V, L, uRoughness);
                vec3 F = FresnelShlick(max(dot(H,V), 0.0), F0);

                vec3 kS = F;
                vec3 kD = 1.0 - kS;
                kD *= 1.0 - uMetallicness;

                vec3 num = NDF * G * F;
                float denom = 4.0 * max(dot(N,V), 0.0) * max(dot(N,L), 0.0) + 0.0001;
                vec3 specular = num/denom;

                //add to outgoing radiance Lo
                float NdotL = max(dot(N, L), 0.0);
                Lo += (kD*result / PI + specular) * radiance * NdotL;
            }

            vec3 ambient = vec3(0.03) * result;
            vec3 color = ambient + Lo;

            color = color / (color + vec3(1.0));
            color = pow (color, vec3(1.0/2.2));

            vec4 linecolor = texture(uLines1, gl_FragCoord.xy/(8.0*float(uHatchSize)) + vec2(cos(gl_FragCoord.x)*0.005, sin(gl_FragCoord.y)*0.005));
            vec4 linecolor2 = texture(uLines2, gl_FragCoord.xy/(8.0*float(uHatchSize)) + vec2(cos(gl_FragCoord.x)*0.005, sin(gl_FragCoord.y)*0.005));
            float intensity = length(color.rgb);

            if (uQuantizeColors){
                float quantizedIntesity = QuantizeChannel(intensity, float(uColorQuantity));
                color = normalize(color.rgb) * quantizedIntesity;
            }

            if (intensity < 0.4 && uCrosshatch){
                color = mix(color, linecolor.rgb, linecolor.w*0.8);
            }
            if (intensity < 0.2 && uCrosshatch) {
                color = mix(color, linecolor2.rgb, linecolor2.w*0.8);
            }


            result = color.rgb;


            return vec4(result, alpha);
        }

        void main() {
            vec4 finalColor =  uBaseColor * Color;
            if (uUseDiffTexture){
                finalColor *= texture(uBaseTexture, TexCoords[0]);
            }

            FragColor = calcPBR(finalColor);
        }
    </script>
  </body>
</html>
